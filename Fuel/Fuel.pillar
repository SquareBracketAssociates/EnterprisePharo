! Serializing Complex Objects as a Breeze with Fuel

Serializing objects is an important domain since objects should be stored and reloaded on different environments. 
There is a plethora of frameworks to serialize objects based on recursive parsing of the object graphs. Some are textual such as XML, JSON, STON. We will present STON and JSON 
in other chapters of this book. However such approaches are often too slow.  Fuel, the framework presented here, is a fast open-source general-purpose binary object serialization framework
developed by M. Martinez-Peck, M. Dias and M. Leske. It is robust and used in many industrial cases. 

Fuel  is based on the idea that objects are much more times loaded than stored, therefore it is worth to spend time while storing to have faster loading and user experience. 
Fuel is in fact one of the fastest object loader and a really fast object saver. In addition Fuel can serialize nearly any object: in fact all the objects
except processes (yes with Fuel you can even serialize a full execution stack and reload it). Fuel is based on a pickle format (that is similar objects are 
clustered together to load faster).


!! General Information

before going into details we present the goals, features and give general information about Fuel. 
Fuel has been developed and maintained over the years but the following people: Martin Dias - tinchodias (at) gmail (dot) com (Developer),
Mariano Martinez Peck - marianopeck (at) gmail (dot) com (developer),  Max Leske (developer), Pavel Krivanek (developer), Tristan Bourgois (past developer)
and StÃ©phane Ducasse (promotor and financer). We want to thank Eliot Miranda for the pickle implementation of VisualWorks parcels that show us that it was a good approach to follow. 

!!!Goals

; Concrete 
: Fuel doesn't aspire to have a dialect-interchange format. This enables us to serialize special objects like contexts, block closures, exceptions, compiled methods and classes. Although there are ports to other dialects, Fuel development is Pharo-centric.

; Flexible
: Depending on the context, there could be multiple ways of serializing the same object. For example, a class can be considered either a global or a regular object. In the former case, it will be encoded just its name; in the latter case, the class will be encoded in detail, with its method dictionary, etc.

; Fast 
: Fuel has been designed to have the best performance. Fuel comes with a complete benchmark suite to help analyse the performance with diverse sample sets, as well as compare against other serializers. Fuel's pickling algorithm allows outstanding materialization performance, as well as very good serialization performance too.

; Object-Oriented Design
: From the beginning it was a constraint to have a good object-oriented design and to do not need any special support from the virtual machine. In addition, Fuel has a complete test suite, with a high coverage. Fuel code is also covered with comments on classes and methods. 

!!! Features

Fuel is a fast, concrete, general-purpose and flexible binary serializer.
- It has an object-oriented design.
- It does not need special VM-support.
- It is modularly packaged.
- It can serialize/materialize not only plain objects but also classes, traits, methods, closures, contexts, packages, etc.
- It supports global references.
- It is very customizable: you ignore certain instance variables, substitute objects by others, define pre and post serialization and materialization actions, etc.
- It supports class rename and class reshape.

Finally Fuel has good test coverage (almost 600 unit tests) and a large suite of benchmarks.

!!! Some links

- Fuel's bugs and issues are tracked at *http://code.google.com/p/fuel/issues/list*.
- Fuel's source code is stored at *http://ss3.gemstone.com/ss/Fuel.html*.
- Fuel is built automatically at *http://ci.inria.fr/pharo-contribution/job/Fuel/*.



%=========================================================================% 

!! Installation
@Installation

Fuel 1.9 is available by default in Pharo since version 2.0 of Pharo.  Therefore you do not need to install it.
The ""default packages"" work out of the box in Pharo 1.1.1, 1.1.2, 1.2, 1.3, 1.4, 2.0, 3.0 and 4.0 and Squeak 4.1, 4.2, 4.3, 4.4, 4.5.

!!!Default
For most users should be enough to install just the default packages:

[[[language=Smalltalk
Gofer it
    squeaksource: 'MetacelloRepository';
    package: 'ConfigurationOfFuel';
    load.
((Smalltalk at: #ConfigurationOfFuel) project version: '1.9') 
    load.
]]]

@@authorToDo SD: is 1.9 the right version, man ?


!!!Basic demo

Open the ==Transcript== and execute code below in a ==Workspace==. This example serializes a set, the default transcript and a block.
It shows that we can serialize them. On reload it shows that the set is correctly recreated and that the block evaluate properly.

[[[language=Smalltalk
| serializedArray materializedArray |
serializedArray := 
    Array 
        with: (Set with: 42)
        with: Transcript
        with: [:aString | Transcript show: aString; cr ].

"Store (serialize)"
FLSerializer serialize: serializedArray toFileNamed: 'demo.fl'. 

"Load (materialize)"
materializedArray := FLMaterializer materializeFromFileNamed: 'demo.fl'. 

Transcript
	show: 'The sets are equal: ';
	show: serializedArray first = materializedArray first;
	cr;
	show: 'But not the same: ';
	show: serializedArray first ~~ materializedArray first;
	cr;
	show: 'The global value Transcript is the same: ';
	show: serializedArray second == materializedArray second;
	cr.
materializedArray third 
	value: 'The materialized block closure can be properly evaluated.'.
]]]


%=========================================================================% 

!! Getting Started
@GettingStarted

!!!Basic examples
Fuel offers some class-side messages to ease more common uses of serialization (==serialize:toFileNamed:==) and materialization. The next example writes and reads from file:

[[[language=Smalltalk
FLSerializer serialize: 'stringToSerialize' toFileNamed: 'demo.fuel'. 
materializedString := FLMaterializer materializeFromFileNamed: 'demo.fuel'.
]]]

Fuel also provides the messages ==serializeToByteArray:== and ==materializeFromByteArray:== for storing into a ==ByteArray==. 
This can be interesting for users of a NoSQL database such Mongo.

[[[language=Smalltalk
anArray := FLSerializer serializeToByteArray: 'stringToSerialize'. 
materializedString := FLMaterializer materializeFromByteArray: anArray.
]]]

!!!FileStream

In the following example we work with files. Note that we set the stream in binary mode:

[[[language=Smalltalk
FileStream forceNewFileNamed: 'demo.fuel' do: [:aStream |
	FLSerializer newDefault 
		serialize: 'stringToSerialize' 
 		on: aStream binary].

FileStream oldFileNamed: 'demo.fuel' do: [:aStream |
	materializedString := (FLMaterializer newDefault 
		materializeFrom: aStream binary) root].
]]]

Note also that we are no longer using the class-side messages of previous examples. 
Now, for both ==FLSerializer== and ==FLMaterializer==, we first create instances with ==newDefault== to then perform 
the desired operations. As we will see in next example, creating the instances brings more possibilities.

!!!Compressing 

Of course, you could use stream compressors provided by the system. However, we have detected some errors serializing ==WideString==s. An example of use:

@@authorToDo SD: I do not get the point on errors....

[[[language=Smalltalk
FileStream forceNewFileNamed: 'number.fuel.zip' do: [:aFileStream | 
	|gzip|
	aFileStream binary.
	gzip := GZipWriteStream on: aFileStream.
	FLSerializer newDefault serialize: 123 on: gzip.
	gzip close. ].
   
FileStream oldFileNamed: 'number.fuel.zip' do: [:aFileStream | 
	|gzip|
	aFileStream binary.
	gzip := GZipReadStream on: aFileStream.		
	materialization := FLMaterializer newDefault 
		materializeFrom: gzip.
	gzip close. ].
]]]

!!!Showing a progress bar

Sometimes it is nice to see progress updates on screen. Use the message ==showProgress== in this case.
Pay attention that you should install the package ==FuelProgressUpdate== to get this behavior.

To load it you can use:

[[[language=Smalltalk
(ConfigurationOfFuel project version: '1.9') 
	load: 'FuelProgressUpdate'.
]]]

The following example uses the message ==showProgress==. 

[[[language=Smalltalk
FileStream forceNewFileNamed: 'numbers.fuel' do: [:aStream |
	FLSerializer newDefault 
		showProgress;
		serialize: (1 to: 200000) asArray 
		on: aStream binary ].
	
FileStream oldFileNamed: 'numbers.fuel' do: [:aStream | 
	FLMaterializer newDefault 
		showProgress;
		materializeFrom: aStream binary ].
]]]



%=========================================================================% 
!! Managing Globals
@ManagingGlobals


Let us assume a ==CompiledMethod== is referenced from the graph to serialize. Sometimes we may be interested in storing just the selector 
and name of the class, because we know it will be present when materializing the graph. However, sometimes we want to really store the 
method with full detail. This means that given an object graph, there is not an unique way of serializing it. Fuel offers dynamic and static mechanisms to customize this.

!!!Default globals

By default, Fuel considers the following objects as globals, i.e., it will store just their name:

- ==nil==, ==true==, ==false==, and ==Smalltalk globals==.
- Any ==Class==, ==Trait==, ==Metaclass== or ==ClassTrait==.
- Any ==CompiledMethod== (except when either it answers false to the message ==isInstalled== or true to the message ==isDoIt==, for example, the code is evaluated from Workspace).
- Some well-known global variables: ==Smalltalk==, ==SourceFiles==, ==Transcript==, ==Undeclared==, ==Display==, ==TextConstants==, ==ActiveWorld==, ==ActiveHand==, ==ActiveEvent==, ==Sensor==, ==Processor==, ==ImageImports==, ==SystemOrganization== and ==World==.

@@authorToDo SD: is the line on compiled method correct

!!!Custom globals are duplicated on materialization

With this following code snippet, we show that by default a global value is not serialized as a global. In such a case it is duplicated on materialization.

[[[language=Smalltalk
 "Define a global variable named SomeGlobal."
 SomeGlobal := Set new.
 
 "Serialize and materialize the value of SomeGlobal."
 FLSerializer 
 	serialize: SomeGlobal 
 	toFileNamed: 'g.fuel'.
 
 "The materialized object *is not* the same as the global instance."
 [ (FLMaterializer materializeFromFileNamed: 'g.fuel') ~~ SomeGlobal ] assert.
]]]

The following explains how we can tell Fuel to handle a new global to avoid its duplication on materialization. 

!!!How to avoid duplication

Instead, in the code below the message ==considerGlobal:== is used to specify that it should be stored as global.

[[[language=Smalltalk
 | aSerializer |
 
 "Define a global variable named SomeGlobal."
 SomeGlobal := Set new.
 
 aSerializer := FLSerializer newDefault.
 
 "Tell the serializer to consider SomeGlobal as global."
 aSerializer analyzer considerGlobal: #SomeGlobal.
 
 aSerializer 
 	serialize: SomeGlobal 
 	toFileNamed: 'g.fuel'.
 	
 "In this case, the materialized object *is* the same as the global instance."
 [ (FLMaterializer materializeFromFileNamed: 'g.fuel') == SomeGlobal ] assert.
]]]

This feature is tested in ==tests-globals== protocol of ==FLBasicSerializationTest== as well in ==FLGlobalEnvironmentTest==.

!!!Changing the environment

It is possible to specify where the global will be looked-up during materialization. The message ==globalEnvironment:== exists for that purpose, as the following example shows. 

[[[language=Smalltalk
 | aSerializer aMaterializer anEnvironment |
 
 "Define a global variable named SomeGlobal."
 SomeGlobal := Set new.
 
 "Tell the serializer to consider SomeGlobal as global."
 aSerializer := FLSerializer newDefault.
 aSerializer analyzer considerGlobal: #SomeGlobal.
 aSerializer 
 	serialize: SomeGlobal 
 	toFileNamed: 'g.fuel'.
 
 "Override value for SomeGlobal."
 anEnvironment := Dictionary newFrom: Smalltalk globals.
 anEnvironment at: #SomeGlobal put: {42}.
 
 "In this case, the materialized object *is the same* as the global instance."
 FileStream oldFileNamed: 'g.fuel' do: [ :aStream |
 	aStream binary.
 	aMaterializer := FLMaterializer newDefault.
 	
 	"Set the environment"
 	aMaterializer globalEnvironment: anEnvironment.
 
 	[ (aMaterializer materializeFrom: aStream) root = {42} ] assert ]
]]]

This feature is tested in the class ==FLGlobalEnvironmentTest==. The global environment can be set also for serialization and not only for materialization, but we don't include an example for that case.


%=========================================================================% 

!! Customizing the Graph
When serializing an object you often want to select the state should be serialized. With Fuel you can specify the variables can be ignored.

!!!Ignoring Instance Variables

It can happen that instance variables should never be serialized. A practical way to do this is overriding the hook method ==fuelIgnoredInstanceVariableNames==. 

Let's say we have the class ==User== and we do not want to serialize the instance variables =='accumulatedLogins'== and =='applications'==. So we implement:

[[[language=Smalltalk
User class >> fuelIgnoredInstanceVariableNames
    ^#('accumulatedLogins' 'applications')
]]]

When materialized, such instance variables will point to ==nil==. If you want to re-initialize and set values to those instance variables, you can use the messahe ==fuelAfterMaterialization== for that.

Be aware that in case of renaming those instance variables, you should revisit that method as well. Notice also that the method ==fuelIgnoredInstanceVariableNames== is implemented on the class side. This means that ""all"" instances of such class will ignore the defined instances variables when serialized. 

We test this feature in ==FLIgnoredVariablesTest==.

%In ==StOMP== serializer this same hook is called ==#stompTransientInstVarNames== and in ==SIXX== it is ==#sixxIgnorableInstVarNames==. 

!!!Post-Materialization Action

The message ==fuelAfterMaterialization== lets us execute some action once an object has been materialized. For example, let's say we would like to set back the instance variable =='accumulatedLogins'== during materialization. Hence, we can implement:

[[[language=Smalltalk
User >> fuelAfterMaterialization
     accumulatedLogins := 0. 
]]]

!!!Substitution on Serialization

Sometimes you may want to serialize something different than the original object, without altering it. Fuel proposes two different ways to do 
this.

!!!!Dynamic way

You can establish a pluggable substitution to a particular serialization. Let's illustrate with an example, where your graph includes a ==Stream== and you want to serialize ==nil== instead. 

[[[language=Smalltalk
objectToSerialize := Array with: 'hello' with: '' writeStream.

FileStream forceNewFileNamed: 'demo.fuel' do: [ :aStream |
    aSerializer := FLSerializer newDefault.
    aSerializer analyzer 
        when: [ :o | o isStream ] 
        substituteBy: [ :o | nil ].
    aSerializer         
        serialize: objectToSerialize
        on: aStream binary ].
]]]

So, when loading you will get ==#('hello' nil)==, without any instance of a stream. You can find this code in ==FLUserGuidesTest>>testPluggableSubstitution==.

!!!! Static way
You can also do it more systematically by override the method ==fuelAccept:== in the class of the object to be substituted. Fuel visits each object in the graph by sending this message to determine how to trace and serialize it. Note that this will affect every serialization, in contrast with the dynamic way we explained above; but it could be much faster.

As an example, imagine we want to replace an object directly with nil. In other words, we want to make a whole object transient, say ==CachedResult==. For that, we should implement:

[[[language=Smalltalk
CachedResult >> fuelAccept: aGeneralMapper
    ^ aGeneralMapper visitSubstitution: self by: nil
]]]

As another example, we have a ==Proxy== class and when serializing we want to serialize its ==target== instead of the proxy. So we redefine ==fuelAccept:== as follows:

[[[language=Smalltalk
Proxy >> fuelAccept: aGeneralMapper
    ^ aGeneralMapper visitSubstitution: self by: target
]]]

The last example is when an object needs to change the value of its instance variables.  Let' imagine that we have again the class ==User== and we want to ==nil== the instance variable =='history'== when its size is greater than 100. 

[[[language=Smalltalk
User >> fuelAccept: aGeneralMapper
    ^self history size > 100 
        ifTrue: [ 
            aGeneralMapper 
                visitSubstitution: self 
                by: (self copy history: Array new) ].
        ifFalse: [ super fuelAccept: aGeneralMapper ]
]]]

""Note"" we are substituting the original user by another instance of ==User==, which Fuel will visit with the same ==fuelAccept:== method. We could easily fall in an infinite sequence of substitutions if we don't take care. To avoid this problem, it is useful to use the message ==visitSubstitution:by:onRecursionDo:==, where you define an alternative mapping for the case of mapping an object which is already a substitute of another one:

[[[language=Smalltalk
User >> fuelAccept: aGeneralMapper
    aGeneralMapper 
        visitSubstitution: self 
        by: (self copy history: #())
        onRecursionDo: [ super fuelAccept: aGeneralMapper ]
]]]

In the case, the substituted user (i.e., the one with the empty history) is will be visited via its super implementation.

You can see tests for this functionality at ==FLHookedSubstitutionTest==.


!!! Substitution on Materialization
In the same way that we may want to customize object serialization, we may want to customize object materialization. 

!!!! Global Sends

Suppose we have a special instance of ==User== that represents the admin user, and it is an unique instance in the image. In case the admin user is referenced in our graph, we want to treat that object as a global. We can do that in this way:

[[[language=Smalltalk
User >> fuelAccept: aGeneralMapper
    ^self == User admin
        ifTrue: [ 
            aGeneralMapper 
                visitGlobalSend: self 
                name: #User 
                selector: #admin ]
        ifFalse: [ super fuelAccept: aGeneralMapper ]
]]]

What will happen is that during serialization, the admin user won't be completely serialized (with all its intance variables) but instead its global name and selector are stored. Then, at materialization time, Fuel will send the message ==admin== to the class ==User==, and use the returned value as the admin user of the materialized graph. 

We test this feature in ==FLGlobalSendSerializationTest==.

!!!! Hooking instance creation

Fuel provides two hook methods to customise how instances are created: ==fuelNew== and ==fuelNew:==.

For (regular) fixed objects, the method ==fuelNew== is defined in ==Behavior== as:

[[[language=Smalltalk
fuelNew
	^ self basicNew
]]]

But we can override it to our needs, for example:

[[[language=Smalltalk
fuelNew
	^ self uniqueInstance
]]]

This similarly applies to variable size objects through the method ==fuelNew:==, which by default answers ==basicNew:==.

We test this feature in ==FLSingletonTest==.

!!!Not Serializable Objects

You may want to be sure that some objects are not serialized. For this case Fuel provides the hook method named ==visitNotSerializable:==, which in next example forbids serialization of any instance of ==MyNotSerializableObject==.

[[[language=Smalltalk
MyNotSerializableObject >> fuelAccept: aGeneralMapper
    aGeneralMapper visitNotSerializable: self
]]]

We test this feature in ==FLBasicSerializationTest>>testNotSerializableObject==.


%=========================================================================% 

!! Errors

We provide a hierarchy of errors which allow one to clearly identify the problem when something went wrong:

- ==FLError==
-- ==FLSerializationError==
--- ==FLNotSerializable==
--- ==FLObjectNotFound==
--- ==FLObsolete==
-- ==FLMaterializationError==
--- ==FLBadSignature==
--- ==FLBadVersion==
--- ==FLClassNotFound==
--- ==FLGlobalNotFound==
--- ==FLMethodChanged==
--- ==FLMethodNotFound==

As most classes of Fuel, they have class comments that give an idea their meanings:

; FLError
: I represent an error produced during Fuel operation.

; FLSerializationError
: I represent an error happened during serialization.

; FLNotSerializable
: I represent an error which may happen while tracing in the graph an object that is forbidden of being serialized.

; FLObjectNotFound
: I represent an error which may happen during serialization, when trying to encode on the stream a reference to an object that should be encoded before, but it is not. This usually happens when the graph changes during serialization. Another possible cause is a bug in the analysis step of serialization.

; FLObsolete
: I am an error produced during serialization, signaled when trying to serialize an obsolete class as global. It is a prevention, because such class is likely to be absent during materialization.

; FLMaterializationError
: I represent an error happened during materialization.

; FLBadSignature
: I represent an error produced during materialization when the serialized signature doesn't match the materializer's signature (accessible via ==FLMaterializer>>signature==). A signature is a byte prefix that should prefix a well-serialized stream.

; FLBadVersion
: I represent an error produced during materialization when the serialized version doesn't match the materializer's version (accessible via ==FLMaterializer>>version==). A version is encoded in 16 bits and is encoded heading the serialized stream, after the signature.

; FLClassNotFound
: I represent an error produced during materialization when a serialized class or trait name doesn't exist.

; FLGlobalNotFound
: I represent an error produced during materialization when a serialized global name doesn't exist (at ==Smalltalk globals==).

; FLMethodChanged
: I represent an error produced during materialization when is detected a change in the bytecodes of a method serialized as global. This error was born when testing the materialization of a ==BlockClosure== defined in a method that changed. The test produced a VM crash.

; FLMethodNotFound
: I represent an error produced during materialization when a serialized method in a class or trait name doesn't exist (at ==Smalltalk globals==).


%=========================================================================% 

!! Object Migration
We often need to load objects whose class has changed since it was saved. Figure *figClassChanges* is useful to explain the possible changes that can happen in a class shape. Imagine we serialized an instance of ==Point== and we need to materialize it when ==Point== class has changed.

+Example of changes to a class>file://figures/ClassChanges.png|width=70|label=figClassChanges+


@@authorToDo SD stopped here


Let's start with the simple cases. If a variable was ""inserted"", its value will be ==nil==. If ""removed"", it is also obvious: the serialized value will be ignored. In the case the variables are the same but where the ""order changed"", Fuel handles it automatically.

A more interesting case is when a variable was ""renamed"". Fuel cannot automatically guess the renaming but the user can specify using the message ===migratedClassNamed:variables:==. The user should provide a mapping from old names to new ones as shown in the following example:

[[[language=Smalltalk
FLMaterializer newDefault
	migrateClassNamed: #Point
	variables: {'x' -> 'posX'. 'y' -> 'posY'}.
]]]

Not surprisingly, if nothing is specified, the change will be understood by Fuel as two independent operations: an insertion and a removal.

The last change that can happen is a ""class rename"". Again Fuel provides a way to handle this case using the message ==migrateClassNamed:toClass:==. 
This should be specified this way:

[[[language=Smalltalk
FLMaterializer newDefault
	migrateClassNamed: #Point
	toClass: Coordinate.
]]]

Fuel defines the message ==migrateClassNamed:toClass:variables:== that combines both ""class and variable rename"".

Although not illustrated in the figure, a class could also change its ""layout"". For example, Point could change from being ""fixed"" to ""variable"". This should be also automatically tolerated by Fuel. Unfortunately, the inverse (variable to fixed) is not supported so far.

You can find tests related to this guide in ==FLMigrationTest==.

Additionally, the method ==globalEnvironment:==, showed in Section *ManagingGlobals*, is useful for migrations: you can prepare an ad-hoc environment dictionary with the same keys that were used during serialization, but with the new classes as values.


%=========================================================================% 

!! Fuel Format Migration

Until now, each Fuel version has its own stream format. Furthermore, each version is ""not"" compatible with the others. This means that when upgrading Fuel version, we will need to convert our serialized streams. 

We include below an example of migration. Let's say we have some files serialized with Fuel 1.7 in a Pharo 1.4 image and we want to migrate them to Fuel 1.9.

[[[language=Smalltalk
 | oldVersion newVersion fileNames objectsByFileName 
   materializerClass serializerClass |
 oldVersion := '1.7'.
 newVersion := '1.9'.
 fileNames := #('a.fuel' 'b.fuel' 'c.fuel' 'd.fuel' 'e.fuel').
 objectsByFileName := Dictionary new.
 
 (ConfigurationOfFuel project version: oldVersion) load.
 materializerClass := Smalltalk at: #FLMaterializer.
 
 fileNames do: [ :fileName | 
 	objectsByFileName 
 		at: fileName 
 		put: (materializerClass materializeFromFileNamed: fileName) ].
 
 (ConfigurationOfFuel project version: newVersion) load.
 serializerClass := Smalltalk at: #FLSerializer.
 
 objectsByFileName keysAndValuesDo: [ :fileName :objects |
 	serializerClass 
 		serialize: objects  
 		toFileNamed: 'migrated-', fileName
 	 ].
]]]

@@note Note 1: We assume in this example that the number of objects to migrate can be materialized all together at the same time. This can be false. In such case, we could fix the script to split the list of files and do it in parts.

@@note Note 2: It is necessary to fetch the classes in the System Dictionary after the desired Fuel version has been loaded.

@@note Note 3: This script should be evaluated in the original image. For example, we don't guarantee that Fuel 1.7 loads in Pharo 2.0, but we know that Fuel 1.9 loads in Pharo 1.4.

%=========================================================================% 

!! Debugging

There are a couple of packages that help us debugging Fuel. To understand the output of the tools, you should know some basics of how Fuel internally works. 

!!!Serialization
The most important thing to know is that serialization is split in two main steps: analysis and encoding.

!!!!Analysis
It consists in a graph iteration, mapping each traversed object to its correspondent grouping, called a ""cluster"". 

!!!!Encoding
After analysis, we linearly write on the stream, in these steps:

#header
#for each cluster, instances part
#for each cluster, references part
#trailer

!!!Materialization
It consists on progressively recreating the graph.

!!!!Decoding
This is done by linearly reading from the stream. So, steps are obviously analogous to the ones above:

#header
#for each cluster, instances part
#for each cluster, references part
#trailer

!!!Debug Tools
Ensure you have them with:

[[[language=Smalltalk
(ConfigurationOfFuel project version: '1.8.1') 
    load: #(FuelDebug FuelPreview).
]]]

Here is a list of some useful class comments. 


!!!!FLGraphViewBuilder
I add draw capabilities to analysis in ==FuelDebug== package.

Right-click a node for inspect it. Some examples:

[[[language=Smalltalk
(FLAnalyzer newDefault
    setDebug;
    analysisFor: #((1) (2) (3) (4)))
    open.
]]]

[[[language=Smalltalk
(FLAnalyzer newDefault
    setDebug;
    analysisFor: #((1) (2) (3) (4)))
    openPathsTo: 3.
   
(FLAnalyzer newDefault
    setDebug;
    analysisFor: #((1) (2) (3) (4)))
    openPathsToEvery: [:o | o isNumber and: [o > 2] ].
]]]

Figure *figFuelPreview* shows how they look like.

+Visual preview of graph to be serialized>file://figures/FuelPreview.png|width=60|label=figFuelPreview+
_

!!!!FLDebugSerialization
I am a serialization which facilitates debugging, by logging the stream position before and after main steps of ==FLSerialization==, including cluster information. Obviously, you should be familiar with such class and the algorithm to understand the output log.

To use, send the message ==setDebug== to your serializer and run as usually. For example:

[[[language=Smalltalk
FileStream forceNewFileNamed: 'debug.fuel' do: [:aFile |
    FLSerializer newDefault
        setDebug;
        serialize: ''hello'' on: aFile binary ]
]]]

Then, inspect the output log:

[[[language=Smalltalk
FLDebugSerialization last log
]]]

!!!!FLDebugMaterialization
I am a materialization which facilitates debugging, by logging the stream position before and after main steps of ==FLMaterialization==, including cluster information. Obviously, you should be familiar with such class and the algorithm to understand the output log.

To use, send the message ==setDebug== to your serializer and run as usually. For example:

[[[language=Smalltalk
FileStream oldFileNamed: 'debug.fuel' do: [:aFile |
    FLMaterializer newDefault
        setDebug;
        materializeFrom: aFile binary ]
]]]

Then, inspect the output log:

[[[language=Smalltalk
FLDebugMaterialization last log
]]]


%=========================================================================% 

!! Built-in Header Support

Since the graph of objects serialized in a file can be large, and it can be useful to query some small extra info, Fuel supports the possibility to easily add such information in a header. The following example shows this feature: First we add a property called timestamp to the header using the message ==at:putAdditionalObject:==. We then define soem pre and post actions. In particular we show how we can use the property value using the message ==additionalObjectAt:== 
 
[[[language=Smalltalk
| serializer |
serializer := FLSerializer newDefault.

serializer header 
	at: #timestamp
	putAdditionalObject: TimeStamp now.

serializer header
	addPreMaterializationAction: [ 
		Transcript show: 'Before serializing'; cr ].

serializer header
	addPostMaterializationAction: [ :materialization | 
		Transcript 
			show: 'Serialized at ';
			show: (materialization additionalObjectAt: #timestamp); 
			cr;
			show: 'Materialized at ';
			show: TimeStamp now; 
			cr ].
	
serializer 
	serialize: 'a big amount of data' 
	toFileNamed: 'demo.fl'
]]]

Then, you can just materialize the header info:

[[[language=Smalltalk
| aHeader |
aHeader := FLMaterializer materializeHeaderFromFileNamed: 'demo.fl'.
aHeader additionalObjectAt: #timestamp.
]]]

Printing it, the result is:

[[[language=Smalltalk
'28 March 2013 12:44:54 pm'
]]]

If we normally materialize the whole file with:

[[[language=Smalltalk
FLMaterializer materializeFromFileNamed: 'demo.fl' 
]]]

Then, the print of the results is:

[[[language=Smalltalk
'a big amount of data'
]]]

And this is shown in ==Transcript==:

[[[language=Smalltalk
Before serializing
Serialized at 28 March 2013 12:50:50 pm
Materialized at 28 March 2013 1:01:21 pm
]]]
 
For additional examples, you can see tests in ==FLHeaderSerializationTest==.


! Conclusion

Fuel is a fast and stable binary object serializer. Some people use Fuel to get information when an error occurs in an application 
that they deployed to a client. In such a case, they serialize the full stack and once they get the file they just load it and open a debugger on the 
saved stack. Fuel has been covered by scientific publications that you can find on *http//rmod.lille.inria.fr* and ou can find more information about Fuel 
on the following web site: *http://rmod.inria.fr/web/software/Fuel*. 